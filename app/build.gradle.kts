import com.android.build.api.dsl.ApplicationExtension
import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.androidx.room)
    alias(libs.plugins.detekt)
    alias(libs.plugins.hilt)
    alias(libs.plugins.ktlint)
}

val rememberApplicationId = "dev.bikram.remember"
val rememberCompileSdk = 36
val rememberMinSdk = 31
val rememberTargetSdk = 36
val versionCode = 10902
val versionName = "1.9.2"

kotlin {
    jvmToolchain(
        libs.versions.java
            .get()
            .toInt(),
    )
    compilerOptions {
        freeCompilerArgs.addAll(
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3ExpressiveApi",
            "-opt-in=androidx.compose.material3.adaptive.ExperimentalMaterial3AdaptiveApi",
            "-opt-in=androidx.compose.animation.ExperimentalAnimationApi",
        )
        providers
            .gradleProperty("kotlin.compiler.metrics.destination")
            .orNull
            ?.takeIf { it.isNotBlank() }
            ?.let { metricsDestination ->
                freeCompilerArgs.addAll(
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=$metricsDestination",
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=$metricsDestination",
                )
            }
    }
}

// Release signing only when keystore.properties exists (CI can write it from secrets).
// Local assembleRelease stays unsigned without that file; sign locally with your own tooling if needed.
val keystoreProps = Properties()
val keystorePropsFile = rootProject.file("keystore.properties")
if (keystorePropsFile.exists()) {
    keystorePropsFile.inputStream().use { keystoreProps.load(it) }
}

val releaseStoreFile =
    keystoreProps
        .getProperty("storeFile")
        ?.takeIf { it.isNotBlank() }
        ?.let { rootProject.file(it) }
        ?.takeIf { it.isFile }
val releaseStorePassword = keystoreProps.getProperty("storePassword")?.takeIf { it.isNotBlank() }
val releaseKeyAlias = keystoreProps.getProperty("keyAlias")?.takeIf { it.isNotBlank() }
val releaseKeyPassword = keystoreProps.getProperty("keyPassword")?.takeIf { it.isNotBlank() }

val hasReleaseSigning =
    releaseStoreFile != null &&
        releaseStorePassword != null &&
        releaseKeyAlias != null &&
        releaseKeyPassword != null
val previewVersionSuffix =
    providers.gradleProperty("previewVersionSuffix").orNull?.takeIf { it.isNotBlank() }

extensions.configure<ApplicationExtension>("android") {
    namespace = rememberApplicationId
    compileSdk = rememberCompileSdk

    defaultConfig.versionCode = versionCode
    defaultConfig.versionName = versionName

    defaultConfig {
        applicationId = rememberApplicationId
        minSdk = rememberMinSdk
        targetSdk = rememberTargetSdk
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables { useSupportLibrary = true }

        buildConfigField("String", "GITHUB_REPO", "\"bikram-agarwal/Remember\"")
        buildConfigField(
            "String",
            "PLAY_STORE_URL",
            "\"https://play.google.com/store/apps/details?id=$rememberApplicationId\"",
        )
        buildConfigField("String", "FLAVOR", "\"github\"")
        buildConfigField("Boolean", "CHECK_UPDATES", "true")
        buildConfigField("Boolean", "USE_PLAY_IN_APP_UPDATES", "false")
        buildConfigField("Boolean", "GOOGLE_TASKS_CONNECT_ENABLED", "true")
    }

    androidResources {
        localeFilters += setOf("en")
    }

    signingConfigs {
        create("debugConfig") {
            storeFile = file("${rootDir}/debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
        if (hasReleaseSigning) {
            create("release") {
                storeFile = releaseStoreFile!!
                storePassword = releaseStorePassword!!
                keyAlias = releaseKeyAlias!!
                keyPassword = releaseKeyPassword!!
            }
        }
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.getByName("debugConfig")
        }
        create("devRelease") {
            initWith(getByName("release"))
            applicationIdSuffix = ".dev"
            versionNameSuffix = "-dev"
            signingConfig = signingConfigs.getByName("debug")
            isDebuggable = false
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
            matchingFallbacks += listOf("release")
        }
        release {
            versionNameSuffix = previewVersionSuffix
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
            signingConfigs.findByName("release")?.let { signingConfig = it }
            ndk {
                debugSymbolLevel = "SYMBOL_TABLE"
            }
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    tasks.matching { it.name.contains("AarMetadata") }.configureEach {
        enabled = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.java.get())
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    packaging {
        jniLibs {
            keepDebugSymbols += "**/libdatastore_shared_counter.so"
        }
    }

    sourceSets {
        getByName("androidTest") {
            assets.directories.add("$projectDir/schemas")
        }
    }
}

val googleTasksGmsDependencies = file("google-tasks-gms.gradle.kts")
if (googleTasksGmsDependencies.isFile) {
    apply(from = googleTasksGmsDependencies)
}

room {
    schemaDirectory("$projectDir/schemas")
}

val copyHelpDoc =
    tasks.register<Copy>("copyHelpDoc") {
        from(rootProject.file("docs/HELP.md"))
        into("src/main/assets")
    }

tasks.named("preBuild") {
    dependsOn(copyHelpDoc)
}

detekt {
    buildUponDefaultConfig = true
    config.setFrom(rootProject.files("config/detekt/detekt.yml"))
}

ktlint {
    android.set(true)
    version.set(libs.versions.ktlint.get())
}

configurations.named("detekt") {
    resolutionStrategy {
        force(
            "io.github.detekt.sarif4k:sarif4k:${libs.versions.sarif4k.get()}",
            "io.github.detekt.sarif4k:sarif4k-jvm:${libs.versions.sarif4k.get()}",
            "io.github.oshai:kotlin-logging:${libs.versions.kotlinLogging.get()}",
        )
    }
}

configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (requested.group == "org.jetbrains.kotlin" && requested.name == "kotlin-metadata-jvm") {
            useVersion(libs.versions.kotlin.get())
            because("Hilt 2.59.2 needs a metadata reader that supports Kotlin 2.4.0 metadata.")
        }
    }
}

dependencies {
    implementation(libs.reorderable)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.process)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.material3.adaptive)
    implementation(libs.androidx.material3.adaptive.layout)
    implementation(libs.androidx.material3.adaptive.navigation)
    implementation(libs.androidx.material3.adaptive.navigation.suite)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    ksp(libs.androidx.room.compiler)
    implementation(libs.hilt.android)
    compileOnly(libs.errorprone.annotations)
    implementation(libs.androidx.hilt.work)
    implementation(libs.androidx.hilt.lifecycle.viewmodel.compose)
    ksp(libs.hilt.compiler)
    ksp(libs.androidx.hilt.compiler)
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.collections.immutable)
    implementation(libs.androidx.biometric)
    implementation(libs.androidx.datastore.preferences)
    implementation(libs.androidx.exifinterface)
    implementation(libs.androidx.glance.appwidget)
    implementation(libs.androidx.glance.material3)
    implementation(libs.androidx.graphics.shapes)
    implementation(libs.material.kolor)
    implementation(libs.coil.compose)
    implementation(libs.androidx.work.runtime.ktx)
    implementation(libs.documentfile)
    implementation(libs.androidx.profileinstaller)
    implementation(libs.androidx.splashscreen)
    debugImplementation(libs.androidx.ui.tooling)
    testImplementation(libs.junit4)
    testImplementation(libs.kotlinx.coroutines.test)
    androidTestImplementation(libs.androidx.room.testing)
    androidTestImplementation(libs.androidx.test.ext.junit)
    androidTestImplementation(libs.androidx.test.runner)
}
